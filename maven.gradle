apply plugin: 'maven-publish'

def currentProject = this

//generate sources task
task sourcesJar(type: Jar) {
    from project.sourceSets.main.allSource
    archiveClassifier = 'sources'
}

//generate javadoc task
task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        maven(MavenPublication) {
            //name and version of the artifact
            group 'fr.viveris'
            artifactId = currentProject.name
            version jnidbus_version
            
            //publish compiled java, sources and javadoc
            from currentProject.components.java
            artifact sourcesJar
            artifact javadocJar
            
            //POM file metadata
            pom {
                name = 'JNIDBus'
                description = 'An efficient and simple wrapper around libdbus'
                licenses {
                    license {
                        name = 'The Academic Free License, Version 2.1'
                        url = 'https://spdx.org/licenses/AFL-2.1.html'
                    }
                }
                developers {
                    developer {
                        id = 'lmascaro'
                        name = 'Mascaro Lucas'
                    }
                }
            }
        }
    }
}
